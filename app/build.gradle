apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

apply plugin: "androidx.navigation.safeargs.kotlin"

def apiKeyProperties = new Properties()
apiKeyProperties.load(new FileInputStream(rootProject.file("apikey.properties")))


def versionString = "0.10.1"
def versionNumber = 17

static def getDate() {
    return new Date().format('yyyy.MMdd')
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"

    defaultConfig {
        applicationId "net.c306.photopress"
        minSdkVersion 26
        targetSdkVersion 29
        versionCode versionNumber
        versionName "${getDate()}.${versionString}"

        // should correspond to key/value pairs inside the file
        buildConfigField("String", "WP_ID", apiKeyProperties['CLIENT_ID'])
        buildConfigField("String", "WP_SECRET", apiKeyProperties['CLIENT_SECRET'])

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        // Export schema location
                        "room.schemaLocation"  : "$projectDir/schemas".toString(),
                        // Incremental processing annotations
                        "room.incremental"     : "true",
                        // Expand * in query to only return values used in return variable type
                        "room.expandProjection": "true"
                ]
            }
        }

    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
//            minifyEnabled true
//            shrinkResources true
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "versionString"

    productFlavors {
        elsa {
            versionName "${getDate()}.${versionString}"
        }

        granny {
            versionName "${getDate()}.${versionString}-granny"
        }
    }

    // To inline the bytecode built with JVM target 1.8 into
    // bytecode that is being built with JVM target 1.6. (e.g. navArgs)
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }

    dataBinding {
        enabled = true
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Core
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    // Networking
    implementation 'com.squareup.okhttp3:okhttp:4.6.0'
    implementation 'com.squareup.retrofit2:retrofit:2.8.1'
    implementation 'com.squareup.retrofit2:converter-gson:2.8.1'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.6.0'

    // Images
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.11.0'
    kapt 'com.github.bumptech.glide:compiler:4.11.0'

    // WorkManager
    def work_version = "1.0.1"
    implementation "android.arch.work:work-runtime-ktx:$work_version"

    // Better logging
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // In-app billing
//    implementation 'com.android.billingclient:billing:2.2.0'

    // Kotlin co-routines
    def co_routine_version = "1.3.0"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$co_routine_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$co_routine_version"

    // Material design
    implementation 'com.google.android.material:material:1.1.0'

    // Navigation component
    implementation "androidx.navigation:navigation-fragment:$nav_version"
    implementation "androidx.navigation:navigation-ui:$nav_version"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    // Lifecycle
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'

    // Settings
    def preference_version = '1.1.1'
    implementation "androidx.preference:preference:$preference_version"
    implementation "androidx.preference:preference-ktx:$preference_version"

    // Viewpager for welcome screen
    implementation "androidx.viewpager2:viewpager2:1.0.0"
    implementation 'com.airbnb.android:lottie:3.4.0'

    // Testing
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
